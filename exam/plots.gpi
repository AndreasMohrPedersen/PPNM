system "mkdir -p Plots"

set terminal svg background "white" size 1400,900
set out "Plots/Results.svg"
set multiplot layout 2,3
#set key below maxrows 2 width -8 font ",11"
set key at 5., -5.5, 0  width 0 font ",11"
set key maxrows 2
#set key spacing 0

#specifications for contourplot
set isosamples 500, 500
set surface 
set contour base
set cntrlabel onecolor format '%8.3g' font ',7' start 2 interval 20
set cntrparam order 8
unset colorbox
set view map
set logscale z 10

#testfunctions:
Beale(x,y) = (1.5-x+x*y)**2+(2.25-x+x*y**2)**2+(2.625-x+x*y**3)**2
Ackley(x,y)= -20*exp(-0.2*sqrt(0.5*(x**2+y**2)))-exp(0.5*(cos(2*pi*x)+cos(2*pi*y)))+exp(1)+20

#Bealeplots
set palette defined (0 "#440154", 0.01 "#46327e", 0.1 "#365c8d", 1 "#277f8e", 10 "#1fa187", 100 "#4ac16d", 1000 "#a0da39", 10000 "#fde725")


set title "Beale function\n Points sampled for 1 ms\n Minimiser: Forward Newton" offset 0,1.5 font ",14"
splot [-4.5:4.5] [-4.5:4.5] Beale(x,y) with pm3d notitle\
	, [-4.5:4.5] [-4.5:4.5] Beale(x,y) with lines lc "black" nosurface title "Beale(x,y)"\
	, 'data/QRDForwardBeale.txt' using 1:2:(1) with points nocontour pt 7 ps 0.6 lc rgb "black" notitle\
	, 'data/QRDForwardBeale.txt' using 1:2:(1) with points nocontour pt 7 ps 0.4 lc rgb "#ffe6c9" title "Quasi-random data"\
	, "data/min&guessBeale.txt" using 1:2:(1) with points nocontour pt 7 ps 0.7 lc rgb "green" title "Minimum"\
	, "data/min&guessBeale.txt" using 3:4:(1) with points nocontour pt 7 ps 0.7 lc rgb "red" title "Initial guess"

set title "Beale function\n Points sampled for 1 ms\n Minimiser: Central Newton" offset 0,1.5 font ",14"
splot [-4.5:4.5] [-4.5:4.5] Beale(x,y) with pm3d notitle\
	, [-4.5:4.5] [-4.5:4.5] Beale(x,y) with lines lc "black" nosurface title "Beale(x,y)"\
	, 'data/QRDCentralBeale.txt' using 1:2:(1) with points nocontour pt 7 ps 0.6 lc rgb "black" notitle\
	, 'data/QRDCentralBeale.txt' using 1:2:(1) with points nocontour pt 7 ps 0.4 lc rgb "#ffe6c9" title "Quasi-random data"\
	, "data/min&guessBeale.txt" using 5:6:(1) with points nocontour pt 7 ps 0.7 lc rgb "green" title "Minimum"\
	, "data/min&guessBeale.txt" using 7:8:(1) with points nocontour pt 7 ps 0.7 lc rgb "red" title "Initial guess"

set title "Beale function\n Points sampled for 1 ms\n Minimiser: Nelder Mead" offset 0,1.5 font ",14"
splot [-4.5:4.5] [-4.5:4.5] Beale(x,y) with pm3d notitle\
	, [-4.5:4.5] [-4.5:4.5] Beale(x,y) with lines lc "black" nosurface title "Beale(x,y)"\
	, 'data/QRDNelderMeadBeale.txt' using 1:2:(1) with points nocontour pt 7 ps 0.6 lc rgb "black" notitle\
	, 'data/QRDNelderMeadBeale.txt' using 1:2:(1) with points nocontour pt 7 ps 0.4 lc rgb "#ffe6c9" title "Quasi-random data"\
	, "data/min&guessBeale.txt" using 9:10:(1) with points nocontour pt 7 ps 0.7 lc rgb "green" title "Minimum"\
	, "data/min&guessBeale.txt" using 11:12:(1) with points nocontour pt 7 ps 0.7 lc rgb "red" title "Initial guess"

#Ackley plots
set palette defined (0 "#440154", 1 "#46327e", 3 "#365c8d", 5 "#277f8e", 8 "#1fa187", 10 "#4ac16d", 12 "#a0da39", 16 "#fde725")

set title "Ackley function\n 100 points sampled\n Minimiser: Forward Newton" offset 0,1.5 font ",14"
splot [-4.5:4.5] [-4.5:4.5] Ackley(x,y) with pm3d notitle\
	, [-4.5:4.5] [-4.5:4.5] Ackley(x,y) with lines lc "black" nosurface title "Ackley(x,y)"\
	, 'data/QRDForwardAckley.txt' using 1:2:(1) with points nocontour pt 7 ps 0.6 lc rgb "black" notitle\
	, 'data/QRDForwardAckley.txt' using 1:2:(1) with points nocontour pt 7 ps 0.4 lc rgb "#ffe6c9" title "Quasi-random data"\
	, "data/min&guessAckley.txt" using 1:2:(1) with points nocontour pt 7 ps 0.7 lc rgb "green" title "Minimum"\
	, "data/min&guessAckley.txt" using 3:4:(1) with points nocontour pt 7 ps 0.7 lc rgb "red" title "Initial guess"

set title "Ackley function\n 100 points sampled\n Minimiser: Central Newton" offset 0,1.5 font ",14"
splot [-4.5:4.5] [-4.5:4.5] Ackley(x,y) with pm3d notitle\
	, [-4.5:4.5] [-4.5:4.5] Ackley(x,y) with lines lc "black" nosurface title "Ackley(x,y)"\
	, 'data/QRDCentralAckley.txt' using 1:2:(1) with points nocontour pt 7 ps 0.6 lc rgb "black" notitle\
	, 'data/QRDCentralAckley.txt' using 1:2:(1) with points nocontour pt 7 ps 0.4 lc rgb "#ffe6c9" title "Quasi-random data"\
	, "data/min&guessAckley.txt" using 5:6:(1) with points nocontour pt 7 ps 0.7 lc rgb "green" title "Minimum found by SGO"\
	, "data/min&guessAckley.txt" using 7:8:(1) with points nocontour pt 7 ps 0.7 lc rgb "red" title "Initial guess"

set title "Ackley function\n 100 points sampled\n Minimiser: Nelder Mead" offset 0,1.5 font ",14"
splot [-4.5:4.5] [-4.5:4.5] Ackley(x,y) with pm3d notitle\
	, [-4.5:4.5] [-4.5:4.5] Ackley(x,y) with lines lc "black" nosurface title "Ackley(x,y)"\
	, 'data/QRDNelderMeadAckley.txt' using 1:2:(1) with points nocontour pt 7 ps 0.6 lc rgb "black" notitle\
	, 'data/QRDNelderMeadAckley.txt' using 1:2:(1) with points nocontour pt 7 ps 0.4 lc rgb "#ffe6c9" title "Quasi-random data"\
	, "data/min&guessAckley.txt" using 9:10:(1) with points nocontour pt 7 ps 0.7 lc rgb "green" title "Minimum found by SGO"\
	, "data/min&guessAckley.txt" using 11:12:(1) with points nocontour pt 7 ps 0.7 lc rgb "red" title "Initial guess"

